buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

Map<String, String> versions = [
        support    : '25.3.1',
        butterknife: '8.5.1',
        dagger     : '2.10',
        retrofit   : '2.3.0',
        javax      : '1.0',
        picasso    : '2.5.2',
        rxandroid  : '2.0.1',
        rxjava     : '2.0.6',
        stetho     : '1.5.0',
        espresso   : '2.2.2',

]

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    def keysPropertiesFile = rootProject.file("keys.properties")
    def keysProperties = new Properties()
    keysProperties.load(new FileInputStream(keysPropertiesFile))

    defaultConfig {
        applicationId "es.iridiobis.popularmovies"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 2
        versionName "1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    android.buildTypes.each { type ->
        type.buildConfigField 'String', 'THE_MOVIE_DB_API_KEY', keysProperties['themoviedbApiKey']
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    annotationProcessor "com.google.dagger:dagger-compiler:$versions.dagger"
    annotationProcessor "com.jakewharton:butterknife-compiler:$versions.butterknife"

    compile "com.android.support:appcompat-v7:$versions.support"
    compile "com.android.support:design:$versions.support"
    compile 'com.android.support.constraint:constraint-layout:1.0.1'
    compile "com.squareup.retrofit2:retrofit:$versions.retrofit"
    compile "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
    compile "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    compile "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"
    compile "io.reactivex.rxjava2:rxjava:$versions.rxjava"
    compile "com.squareup.picasso:picasso:$versions.picasso"
    compile "com.google.dagger:dagger-android:$versions.dagger"
    compile "com.jakewharton:butterknife:$versions.butterknife"
    compile "com.facebook.stetho:stetho:$versions.stetho"
    compile "com.facebook.stetho:stetho-okhttp3:$versions.stetho"

    provided "javax.annotation:jsr250-api:$versions.javax"

    // Testing-only dependencies
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'
    androidTestCompile "com.squareup.retrofit2:retrofit-mock:$versions.retrofit"
    androidTestCompile('com.squareup.okhttp:mockwebserver:2.7.2')
    // Force usage of support annotations in the test app, since it is internally used by the runner module.
    androidTestCompile "com.android.support:support-annotations:$versions.support"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile "com.android.support.test.espresso:espresso-core:$versions.espresso"
    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$versions.espresso") {
        exclude module: 'design'
        exclude module: 'appcompat-v7'
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    // UiAutomator Testing
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile 'org.hamcrest:hamcrest-integration:1.3'
}
