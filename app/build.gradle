buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

Map<String, String> versions = [
        support         : '25.3.1',
        butterknife     : '8.5.1',
        dagger          : '2.0.1',
        retrofit        : '2.0.0-beta1',
        javax_annotation: '10.0-b28',
        guava           : '19.0-rc1',
        picasso         : '2.5.2',
        rxjava          : '1.0.14',
        rxandroid       : '1.0.1',
        stetho          : '1.2.0'
]

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    def keysPropertiesFile = rootProject.file("keys.properties")
    def keysProperties = new Properties()
    keysProperties.load(new FileInputStream(keysPropertiesFile))

    defaultConfig {
        applicationId "es.iridiobis.popularmovies"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    android.buildTypes.each { type ->
        type.buildConfigField 'String', 'THE_MOVIE_DB_API_KEY', keysProperties['themoviedbApiKey']
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    annotationProcessor "com.google.dagger:dagger-compiler:$versions.dagger"
    annotationProcessor "com.jakewharton:butterknife-compiler:$versions.butterknife"

    compile "com.android.support:appcompat-v7:$versions.support"
    compile "com.android.support:design:$versions.support"
    compile "com.squareup.retrofit:retrofit:$versions.retrofit"
    compile "com.squareup.retrofit:adapter-rxjava:$versions.retrofit"
    compile "com.squareup.retrofit:converter-gson:$versions.retrofit"
    compile "io.reactivex:rxandroid:$versions.rxandroid"
    compile "io.reactivex:rxjava:$versions.rxjava"
    compile "com.google.guava:guava:$versions.guava"
    compile "com.squareup.picasso:picasso:$versions.picasso"
    compile "com.google.dagger:dagger:$versions.dagger"
    compile "com.jakewharton:butterknife:$versions.butterknife"
    compile "com.facebook.stetho:stetho:$versions.stetho"
    compile "com.facebook.stetho:stetho-okhttp:$versions.stetho"

    provided "org.glassfish:javax.annotation:$versions.javax_annotation"


    compile "com.android.support:support-v4:$versions.support"
    compile "com.android.support:design:$versions.support"

    // Testing-only dependencies
    androidTestCompile "com.squareup.retrofit:retrofit-mock:$versions.retrofit"
    androidTestCompile('com.squareup.okhttp:mockwebserver:2.5.0')
    // Force usage of support annotations in the test app, since it is internally used by the runner module.
    androidTestCompile "com.android.support:support-annotations:$versions.support"
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
    }
    // UiAutomator Testing
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
    androidTestCompile 'org.hamcrest:hamcrest-integration:1.3'
}
